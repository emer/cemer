#!/bin/csh
# script to convert from pdp++ v.3 to v.4

if ($#argv < 2) then
    echo "Need first arg of type of project (e.g., Leabra, Bp, etc.)"
    exit 1
endif

set projtyp=$1
set projnm="V3"$projtyp"Project"
set projgp=$projnm"_Group"
set projection=$projnm"ion"
set netnm=$projtyp"Network"
set wiznm=$projtyp"Wizard"
set netgp=$netnm"_Group"
set wizgp=$wiznm"_Group"

set cvttmp = "pdp_v3_cvt_tmp.xxx"

foreach i (${argv[2-]})
  echo $i
  set ext=$i:e
  set fl=$i
  if($ext == "gz") then
    set fl=$i:r
    gunzip $i
  endif
  set newfl=$fl:r"_v4.proj"

  sed -e "s/_MGroup/_Group/g" \
    -e "s/Project/$projnm/g" -e "s/$projgp/Project_Group/g" -e "s/$projection/Projection/g" \
    -e "s/Network/$netnm/g" -e "s/$netgp/Network_Group/g" \
    -e "s/LeabraWiz/Wizard/g" -e "s/Wizard/$wiznm/g" -e "s/$wizgp/Wizard_Group/g" \
    -e "s/WinView_Group/taBase_Group/g" -e "s/ProjViewState_List/taBase_List/g" \
    -e "s/NetView/taNBase/g" -e "s/DataTable/taNBase/g" -e "s/EnviroView/taNBase/g" \
    $fl >$cvttmp
  mv -f $cvttmp $newfl
  if($ext == "gz") then
    gzip $newfl
  endif
end
